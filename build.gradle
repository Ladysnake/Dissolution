// Credits to UpcraftLP for the base build script


import java.time.Year
import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id 'fabric-loom' version '0.7.29'   // 0.7.30 breaks mixins in prod
    id 'net.minecrell.licenser' version '0.4.1'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.jfrog.artifactory' version '4.21.0'
    id 'org.ajoberstar.grgit' version '3.1.1'
    id 'com.github.breadmoirai.github-release' version '2.2.9'
    id 'com.modrinth.minotaur' version '1.1.0'
    id 'maven-publish'
}

group = rootProject.maven_group
archivesBaseName = rootProject.name
version = mod_version

subprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'com.matthewprenger.cursegradle'
    apply plugin: 'maven-publish'
    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'com.jfrog.artifactory'
}

allprojects {

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    version = rootProject.mod_version

    repositories {
        mavenLocal()
        mavenCentral()
    }

    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from(sourceSets.main.output) {
            include 'fabric.mod.json'
            expand 'version': project.version
        }
        from(sourceSets.main.output) {
            exclude 'fabric.mod.json'
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    apply from: rootProject.file('utilities.gradle')

    dependencies {
        //to change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // Fabric API. This is technically optional, but you probably want it anyway.
        modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

        compileOnly "com.google.code.findbugs:jsr305:3.0.2"
        compileOnly "org.jetbrains:annotations:${jb_annotations_version}"
        compileOnly "org.apiguardian:apiguardian-api:${apiguardian_version}"
        compileOnly "com.demonwav.mcdev:annotations:1.0"

        testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.0-M1"
    }

    license {
        header = rootProject.file("code_quality/${project.license_header}_HEADER.txt")

        matching('ladysnake/**/api/**') {
            header = rootProject.file("code_quality/${project.api_license_header}_HEADER.txt")
        }

        include '**/*.java'
        exclude '**/package-info.java'

        //export variables
        ext {
            year = Year.now()
            projectDisplayName = rootProject.display_name
            projectOwners = rootProject.owners
            gplVersion = rootProject.gpl_version
        }

        newLine = false // Disables the empty line between the header and package name
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact(remapJar)
                artifact(sourcesJar) {
                    builtBy(remapSourcesJar)
                }

                if (project.tasks.findByName('javadocJar')) {
                    artifact project.tasks.getByName('javadocJar')
                }
            }
        }
    }
}

repositories {
    jcenter()
    maven {
        name = 'Ladysnake Mods 1'
        url = 'https://dl.bintray.com/ladysnake/libs'
        content {
            includeGroup 'io.github.ladysnake'
        }
    }
    maven {
        name = 'Ladysnake Mods 2'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroupByRegex 'io\\.github\\.onyxstudios.*'
        }
    }
    maven {
        name = 'CottonMC'
        url = 'http://server.bbkr.space:8081/artifactory/libs-release'
        content {
            includeGroup 'io.github.cottonmc'
        }
    }
    maven {
        url = 'https://maven.jamieswhiteshirt.com/libs-release/'
        content {
            includeGroup 'com.jamieswhiteshirt'
        }
    }
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = "AdrianTodt's Maven"
        url = "https://dl.bintray.com/adriantodt/maven"
        content {
            includeGroup "net.adriantodt.fabricmc"
        }
    }
    maven {
        url 'https://maven.blamejared.com'
        content {
            includeGroupByRegex 'vazkii\\..*'
        }
    }
    maven {
        name = 'Earthcomputer Mods'
        url = 'https://dl.bintray.com/earthcomputer/mods'
        content {
            includeGroup 'net.earthcomputer'
        }
    }
    maven {
        url = "https://maven.shedaniel.me/"
        content {
            includeGroupByRegex 'me\\.shedaniel\\..*'
            includeGroup 'me.sargunvohra.mcmods'
        }
    }
    maven {
        url = "https://maven.theillusivec4.top"
        content {
            includeGroupByRegex 'top\\.theillusivec4\\..*'
        }
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
        content {
            includeGroup 'me.lucko'
        }
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
        content {
            includeGroupByRegex '(io|com)\\.github\\..*'
        }
    }
    mavenLocal()
}

dependencies {
    compile project(":requiem-api")
    // including too early causes a circular dependency somehow
    afterEvaluate {
        include project(":requiem-api")
    }

    modApi("io.github.ladysnake:Satin:${rootProject.satin_version}")
    include "io.github.ladysnake:Satin:${satin_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cca_version}"
    modImplementation ("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cca_version}")
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cca_version}"
    modImplementation ("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-scoreboard:${cca_version}")
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-scoreboard:${cca_version}"
    modImplementation ("io.github.ladysnake:PlayerAbilityLib:${pal_version}")
    include "io.github.ladysnake:PlayerAbilityLib:${pal_version}"
    implementation "me.zeroeightsix:fiber:${project.fiber_version}"
    include "me.zeroeightsix:fiber:${project.fiber_version}"
    modCompileOnly ("io.github.prospector:modmenu:${modmenu_version}") { transitive = false }
    modRuntime ("io.github.prospector:modmenu:${modmenu_version}") { transitive = false }
    modCompileOnly ("me.shedaniel.cloth:cloth-config-fabric:${cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modRuntime ("me.shedaniel.cloth:cloth-config-fabric:${cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly ("me.shedaniel.cloth:fiber2cloth:${f2c_version}") { transitive = false }
    modRuntime ("me.shedaniel.cloth:fiber2cloth:${f2c_version}") { transitive = false }
    include ("me.shedaniel.cloth:fiber2cloth:${f2c_version}") { transitive = false }

//    modRuntime curse.resolve('immersive-portals-mod', 2845123)
//    modRuntime "curse.maven:mobz:2965825"
    modCompileOnly("io.github.Terrails:Health-Overlay:1.16-Fabric-SNAPSHOT")
    modCompileOnly("io.github.CerulanLumina:better-graves:${better_graves_version}")
    modRuntime("io.github.CerulanLumina:better-graves:${better_graves_version}")

    modRuntime "com.jamieswhiteshirt:reach-entity-attributes:${reach_version}"
    modRuntime "net.adriantodt.fabricmc:fallflyinglib:${fallflyinglib_version}"
    modCompileOnly("com.github.Virtuoel:Pehkui:${pehkui_version}") { transitive = false }
    modRuntime("com.github.Virtuoel:Pehkui:${pehkui_version}") { transitive = false }
    modCompileOnly("io.github.Ladysnake:Snow-Mercy:${snowmercy_version}") { transitive = false }
    modRuntime("io.github.Ladysnake:Snow-Mercy:${snowmercy_version}") { transitive = false }
    modCompileOnly("com.github.apace100:origins-fabric:${origins_version}") { transitive = false }
    modRuntime("com.github.apace100:origins-fabric:${origins_version}") { transitive = false }
    modCompileOnly("io.github.HyperPigeon:Eldritch-Mobs:${eldritch_mobs_version}") { transitive = false }
    modRuntime "io.github.cottonmc:LibGui:${libgui_version}"
    modRuntime "net.earthcomputer:libstructure:1.5"
    modCompileOnly("curse.maven:haema-391257:${haema_version}")
    modRuntime("curse.maven:haema-391257:${haema_version}")
    modRuntime("vazkii.patchouli:Patchouli:${project.patchouli_version}")
    modRuntime("me.lucko:fabric-permissions-api:${fpa_version}")    // required by haema
    modRuntime("top.theillusivec4.somnus:somnus-fabric:${somnus_version}")  // required by haema
    modRuntime("me.sargunvohra.mcmods:autoconfig1u:${project.acu_version}")
    modRuntime("net.fabricmc:fabric-language-kotlin:${flk_version}")
    modRuntime("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-world:${rootProject.cca_version}")
    modCompileOnly("curse.maven:golems-galore-387197:${golems_galore_version}")
    modCompileOnly("curse.maven:the-bumblezone-fabric-363949:${bumblezone_version}")
}

remapJar.dependsOn(project(":requiem-api").tasks.remapJar)

jar {
    from 'LICENSE-CODE'
    from 'LICENSE-ART'
}

license {
    exclude '**/RayHelper.java'
}

ext {
    // no idea how else to import TaskModrinthUpload from release.gradle
    ModrinthUpload = TaskModrinthUpload
}

apply from: 'release.gradle'
